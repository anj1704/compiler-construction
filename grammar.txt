<program> ---> <otherFunctions> <mainFunction> 

<mainFunction> ---> TK_MAIN <stmts> TK_END 

<otherFunctions> ---> <function> <otherFunctions> 
<otherFunctions> ---> EPS 

<function> ---> TK_FUNID <inputPar> <outputPar> TK_SEM <stmts> TK_END 

<stmts> ---> <typeDefinitions> <declarations> <otherStmts> <returnStmt> 

<otherStmts> ---> <stmt> <otherStmts> 
<otherStmts> ---> EPS 

<stmt> ---> <assignmentStmt> 
<stmt> ---> <iterativeStmt> 
<stmt> ---> <conditionalStmt> 
<stmt> ---> <ioStmt> 
<stmt> ---> <funCallStmt> 

<parameterList> ---> <dataType> TK_ID <remainingList> 

<inputPar> ---> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameterList> TK_SQR 

<outputPar> ---> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameterList> TK_SQR 
<outputPar> ---> EPS 

<inputParameters> ---> TK_SQL <idList> TK_SQR 

<outputParameters> ---> TK_SQL <idList> TK_SQR TK_ASSIGNOP 
<outputParameters> ---> EPS 

<declarations> ---> <declaration> <declarations> 
<declarations> ---> EPS 

<declaration> ---> TK_TYPE <dataType> TK_COLON TK_ID <globalOrNot> TK_SEM 

<globalOrNot> ---> TK_COLON TK_GLOBAL 
<globalOrNot> ---> EPS 

<dataType> ---> <primitiveDatatype> 
<dataType> ---> <constructedDatatype> 

<primitiveDatatype> ---> TK_INT 
<primitiveDatatype> ---> TK_REAL 

<constructedDatatype> ---> TK_RECORD TK_RUID 
<constructedDatatype> ---> TK_UNION TK_RUID 
<constructedDatatype> ---> TK_RUID 

<typeDefinitions> ---> <actualOrRedefined> <typeDefinitions> 
<typeDefinitions> ---> EPS 

<typeDefinition> ---> TK_RECORD TK_RUID <fieldDefinitions> TK_ENDRECORD 
<typeDefinition> ---> TK_UNION TK_RUID <fieldDefinitions> TK_ENDUNION 

<definetypestmt> ---> TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID 

<actualOrRedefined> ---> <typeDefinition> 
<actualOrRedefined> ---> <definetypestmt> 

<fieldDefinitions> ---> <fieldDefinition> <fieldDefinition> <moreFields> 

<fieldDefinition> ---> TK_TYPE <fieldtype> TK_COLON TK_FIELDID TK_SEM 

<fieldtype> ---> <primitiveDatatype> 
<fieldtype> ---> <constructedDatatype> 

<moreFields> ---> <fieldDefinition> <moreFields> 
<moreFields> ---> EPS 

<assignmentStmt> ---> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM 

<funCallStmt> ---> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM 

<iterativeStmt> ---> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE 

<conditionalStmt> ---> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> <elsePart> 

<ioStmt> ---> TK_READ TK_OP <var> TK_CL TK_SEM 
<ioStmt> ---> TK_WRITE TK_OP <var> TK_CL TK_SEM 

<returnStmt> ---> TK_RETURN <optionalReturn> TK_SEM 

<optionalReturn> ---> TK_SQL <idList> TK_SQR 
<optionalReturn> ---> EPS 

<elsePart> ---> TK_ELSE <stmt> <otherStmts> TK_ENDIF 
<elsePart> ---> TK_ENDIF 

<arithmeticExpression> ---> <term> <expPrime> 

<expPrime> ---> <lowPrecedenceOperators> <term> <expPrime> 
<expPrime> ---> EPS 

<term> ---> <factor> <termPrime> 

<termPrime> ---> <highPrecedenceOperators> <factor> <termPrime> 
<termPrime> ---> EPS 

<factor> ---> TK_OP <arithmeticExpression> TK_CL 
<factor> ---> <var> 

<booleanExpression> ---> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL 
<booleanExpression> ---> <var> <relationalOp> <var> 
<booleanExpression> ---> TK_NOT TK_OP <booleanExpression> TK_CL 

<highPrecedenceOperators> ---> TK_MUL 
<highPrecedenceOperators> ---> TK_DIV 

<lowPrecedenceOperators> ---> TK_PLUS 
<lowPrecedenceOperators> ---> TK_MINUS 

<logicalOp> ---> TK_AND 
<logicalOp> ---> TK_OR 

<relationalOp> ---> TK_LT 
<relationalOp> ---> TK_LE 
<relationalOp> ---> TK_EQ 
<relationalOp> ---> TK_GT 
<relationalOp> ---> TK_GE 
<relationalOp> ---> TK_NE 

<var> ---> <singleOrRecId> 
<var> ---> TK_NUM 
<var> ---> TK_RNUM 

<idList> ---> TK_ID <moreIds> 

<moreIds> ---> TK_COMMA <idList> 
<moreIds> ---> EPS 

<singleOrRecId> ---> TK_ID <optionSingleConstructed> 

<optionSingleConstructed> ---> <oneExpansion> <moreExpansions> 
<optionSingleConstructed> ---> EPS 

<A> ---> TK_RECORD 
<A> ---> TK_UNION 

<remainingList> ---> TK_COMMA <parameterList> 
<remainingList> ---> EPS 

<oneExpansion> ---> TK_DOT TK_FIELDID 

<moreExpansions> ---> <oneExpansion> <moreExpansions> 
<moreExpansions> ---> EPS 

